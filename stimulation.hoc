// Sub-file of setup.hoc to create the extracelular simulation
/* This file create a single pulse starting at START, of duration DUR and with a stimulation current of AMP [ÂµA]*/


// Set the extracelular potential to be equal to the distribution from COMSOL

proc setPotential(){
	for j = 0, naxons-1{
		for i = 0,axons[j].nnode-1{
			axons[j].node[i].e_extracellular = V.x[i]*AMP
		}
	}
}



// Set extracellular to 0 (stimulation off)
proc setPotential0(){
	for j = 0, naxons-1{
		for i = 0, axons[j].nnode-1{
			axons[j].node[i].e_extracellular = 0
		}
	}
}

// Handle the switching of ON/OFF
objref fih
fih = new FInitializeHandler("initi()")

proc initi() {
	STIMON = 0
	cvode.event(START, "seti()")
}

proc seti() {
	if (STIMON==0) {
    STIMON = 1
    {setPotential()}
    cvode.event(t +DUR, "seti()")
  } else {
    STIMON = 0
    {setPotential0()}
  }
  // we've changed a parameter abruptly
  // so we really should re-initialize cvode
  if (cvode.active()) {
    cvode.re_init()
  } else {
    fcurrent()
  }
}